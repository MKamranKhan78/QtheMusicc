apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.crashlytics'
// Un comment below line to add enable firebase
//apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"
    def appVersion = '1.0.0'
    def appCenterBuildID = "${System.env.APPCENTER_BUILD_ID}" != "null" ? "${System.env.APPCENTER_BUILD_ID}" : "1"
    
    defaultConfig {
        applicationId "com.techswivel.baseproject"
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 1
        versionName appVersion
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField("int", "APPCENTER_BUILD_ID", appCenterBuildID)
    }

    buildTypes {
        release {
            manifestPlaceholders = [enableCrashReporting: "true"]
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    outputFileName = new File("{Base Project}_v{" + variant.versionName + "}(" + android.defaultConfig.buildConfigFields["APPCENTER_BUILD_ID"].value + ")" + ".apk")
                }
            }
        }
        debug
                {
                    manifestPlaceholders = [enableCrashReporting: "false"]
                    applicationVariants.all { variant ->
                        variant.outputs.all { output ->
                            outputFileName = new File("Base_project-v{" + variant.versionName + "}(" + android.defaultConfig.buildConfigFields["APPCENTER_BUILD_ID"].value + ")" + ".apk")
                        }
                    }
                }
    }

    flavorDimensions "version"
    productFlavors {
        production {
            dimension "version"
            manifestPlaceholders = [
                    appIcon: "@mipmap/app_icon",
            ]
        }

        development {
            dimension "version"
            applicationIdSuffix ".development"
            versionName appVersion+"-development"
            manifestPlaceholders = [
                    appIcon: "@mipmap/app_icon_development",
            ]
        }
        staging {
            dimension "version"
            applicationIdSuffix ".staging"
            versionName appVersion+"-staging"
            manifestPlaceholders = [
                    appIcon: "@mipmap/app_icon_staging",
            ]
        }
        acceptance {
            dimension "version"
            applicationIdSuffix ".acceptance"
            versionName appVersion+"-acceptance"
            manifestPlaceholders = [
                    appIcon: "@mipmap/app_icon_acceptance",
            ]
        }
    }

    buildFeatures{
        dataBinding = true
        viewBinding = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

dependencies {

    def dagger2Version = '2.25.2'

    implementation platform('com.google.firebase:firebase-bom:26.2.0')
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    //material Support
    implementation 'com.google.android.material:material:1.3.0-beta01'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    //dataStore
    implementation "androidx.datastore:datastore-preferences:1.0.0-alpha01"

    //multiDex
    implementation 'androidx.multidex:multidex:2.0.1'

    //phonumber validation
    implementation 'io.michaelrocks:libphonenumber-android:8.12.6'

    //Google map
    implementation 'com.google.android.libraries.places:places:2.4.0'
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    implementation 'com.google.android.gms:play-services-places:17.0.0'
    implementation 'com.google.android.gms:play-services-drive:17.0.0'
    implementation 'com.google.android.gms:play-services-auth:19.0.0'
    implementation 'com.google.android.gms:play-services-location:17.1.0'
    implementation 'com.google.android.gms:play-services-gcm:17.0.0'

    //fireBase sdk
    implementation 'com.google.firebase:firebase-auth-ktx'
    implementation 'com.google.firebase:firebase-config-ktx'
    implementation 'com.google.firebase:firebase-firestore-ktx'
    implementation 'com.google.firebase:firebase-messaging-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'

    //viewModel
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation "android.arch.lifecycle:common-java8:1.1.1"

    kapt 'com.squareup.inject:assisted-inject-processor-dagger2:0.5.2'
    kapt "com.google.dagger:dagger-compiler:$dagger2Version"
    kapt "com.google.dagger:dagger-android-processor:$dagger2Version"
    implementation "com.google.dagger:dagger-android-support:$dagger2Version"


    //facebook sdk
    implementation 'com.facebook.android:facebook-android-sdk:8.1.0'


    //loading circle Image
    implementation 'de.hdodenhof:circleimageview:3.1.0'

    //Rxjava
    implementation 'io.reactivex.rxjava2:rxjava:2.2.19'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    //retrofit
    implementation 'com.squareup.okio:okio:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.7.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.7.1'

    // glide to
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    kapt 'com.github.bumptech.glide:compiler:4.11.0'
    implementation("io.coil-kt:coil:1.1.0")



}